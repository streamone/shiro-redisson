apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.7
targetCompatibility = 1.7

group = 'com.github.streamone'
version = '1.0.0.SNAPSHOT'

repositories {
    mavenCentral()
}

configurations {
	all*.exclude group: 'apache', module: 'commons-logging'
	all*.exclude group: 'commons-logging', module: 'commons-logging'
}

dependencies {
  compile 'org.apache.commons:commons-lang3:3.3.2',
    'org.slf4j:slf4j-api:1.7.25',
    'org.springframework:spring-core:3.2.18.RELEASE',
    'org.springframework:spring-beans:3.2.18.RELEASE',
    'org.springframework:spring-context:3.2.18.RELEASE',
    'org.apache.shiro:shiro-core:1.3.2',
    'org.redisson:redisson:2.10.5'

  testCompile 'junit:junit:4.12',
    'org.springframework:spring-test:3.2.18.RELEASE'

  testRuntime 'org.slf4j:jcl-over-slf4j:1.7.25',
    'ch.qos.logback:logback-core:1.1.11',
    'ch.qos.logback:logback-classic:1.1.11',
    'ch.qos.logback:logback-access:1.1.11'
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.3.1'
}

javadoc {
    options.encoding = "UTF-8"
    options.locale = "en_US"
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier "sources"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc
    classifier "javadoc"
}

artifacts {
    archives javadocJar
    archives sourceJar
}

signing {
    sign configurations.archives
}

tasks.withType(Sign) {
    onlyIf { !version.endsWith("SNAPSHOT") && project.hasProperty("publish_sign") }
}

